apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
}

task releaseJar(type: Copy, dependsOn: build) {
    from('build/intermediates/bundles/release/')
    into('out/')
    include('classes.jar')
    rename('classes.jar', 'androidcharts.jar')
}

task applyToDemo(type: Copy, dependsOn: releaseJar) {
    from ('out/')
    into ('../bin/androidchartsdemo/libs')
}

task releaseLib(type: Copy, dependsOn: releaseJar) {
    from ('out/')
    into ('../bin/')
    include('androidcharts.jar')
    rename('androidcharts.jar', 'androidcharts.v0.2.2.jar')
}

task releaseJavaDoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../doc/javadoc")
    options {
        links "http://docs.oracle.com/javase/7/docs/api/"
        charSet "utf-8"
        docEncoding "utf-8"
        noDeprecated true
    }
    title = "Android-Charts 0.2.2 Reference"
    failOnError false
}